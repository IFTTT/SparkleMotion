buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.0.0"

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0.0"
    }
}

def siteUrl = 'https://github.com/IFTTT/SparkleMotion'
def gitUrl = 'https://github.com/IFTTT/SparkleMotion.git'
def issueUrl = 'https://github.com/IFTTT/SparkleMotion/issues'
group = "com.ifttt"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                name 'Sparkle Motion'
                url siteUrl

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'lzanita09'
                        name 'Zhe Lu'
                        email 'zhe@ifttt.com'
                    }

                    developer {
                        id 'NightlyNexus'
                        name 'Eric Cochran'
                        email 'eric@ifttt.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.0.1'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'junit:junit:4.12'
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/checkstyle.xml")
    source 'src/main/java/com/ifttt/sparklemotion'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

bintray {
    configurations = ['archives']

    publish = true

    pkg {
        repo = 'maven'
        name = 'sparklemotion'
        userOrg = 'ifttt'
        licenses = ['MIT']
        labels = ['android', 'animation', 'IFTTT', 'sparklemotion']
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
    }
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey
}

